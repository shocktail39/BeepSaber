# This script contains the button logic for the controller
extends XRController3D
class_name BeepSaberController


var ax := false
var ax_last_frame := false
var by := false
var by_last_frame := false
var trigger := false
var trigger_last_frame := false

var movement_aabb := AABB()

func ax_pressed() -> bool:
	return ax

func ax_just_pressed() -> bool:
	return ax and not ax_last_frame

func ax_just_released() -> bool:
	return ax_last_frame and not ax

func by_pressed() -> bool:
	return by

func by_just_pressed() -> bool:
	return by and not by_last_frame

func by_just_released() -> bool:
	return by_last_frame and not by

func trigger_pressed() -> bool:
	return trigger

func trigger_just_pressed() -> bool:
	return trigger and not trigger_last_frame

func trigger_just_released() -> bool:
	return trigger_last_frame and not trigger

func _update_buttons_and_sticks() -> void:
	ax_last_frame = ax
	by_last_frame = by
	trigger_last_frame = trigger
	ax = is_button_pressed(&"ax_button")
	by = is_button_pressed(&"by_button")
	trigger = is_button_pressed(&"trigger")

func _update_movement_aabb() -> void:
	movement_aabb = movement_aabb.expand(global_transform.origin)

func reset_movement_aabb() -> void:
	movement_aabb = AABB(global_transform.origin, Vector3.ZERO)

var _rumble_intensity := 0.0
var _rumble_duration := -128.0 #-1 means deactivated so applications can also set their own rumble

func simple_rumble(intensity: float, duration: float) -> void:
	_rumble_intensity = intensity;
	_rumble_duration = duration;
	trigger_haptic_pulse("haptic", 20, intensity, duration, 0)
	
func is_simple_rumbling() -> bool:
	return _rumble_duration > 0.0
	
func _update_rumble(dt: float) -> void:
	if _rumble_duration < -100: return
	simple_rumble(_rumble_intensity, _rumble_duration)
	_rumble_duration -= dt
	if _rumble_duration <= 0.0:
		_rumble_duration = -128.0
		simple_rumble(0.0, _rumble_duration)

var first_time := true

func _physics_process(dt: float) -> void:
	if not Scoreboard.paused:
		_update_movement_aabb()
	
	if get_is_active(): # wait for active controller
		_update_rumble(dt)
		_update_buttons_and_sticks()
		# this avoid getting just_pressed events when a key is pressed and the controller becomes
		# active (like it happens on vr.scene_change!)
		if first_time:
			_update_buttons_and_sticks()
			first_time = false
